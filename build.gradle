#!groovy
// The above triggers groovy syntax highlighting in vim

plugins {
    id "org.jetbrains.intellij" version "0.1.10"
    id "com.github.hierynomus.license" version "0.12.1"
}

apply plugin: 'java'

allprojects {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
    tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }
}

sourceSets {
    main {
        java.srcDirs 'src/main/java'
        resources.srcDir 'src/main/resources'
    }
    test {
        java.srcDirs 'src/test/java'
        resources.srcDir 'src/test/resources'
    }
}

apply plugin: 'org.jetbrains.intellij'

intellij {
    version = project.hasProperty('ideaVersion') ? ideaVersion : 'IC-2016.2'

    plugins 'android'

//    alternativeIdePath '/Applications/Android Studio.app/Contents'
//    alternativeIdePath '/opt/idea-IC-135.1306'
    alternativeIdePath '/opt/idea-IU-143.382.35'
//    alternativeIdePath '/opt/android-studio'
    pluginName 'samebug-idea-plugin'
    updateSinceUntilBuild false

    publish {
        username project.hasProperty('JETBRAINS_USERNAME') ? project.getProperty('JETBRAINS_USERNAME') : null
        password project.hasProperty('JETBRAINS_PASSWORD') ? project.getProperty('JETBRAINS_PASSWORD') : null
        channel project.hasProperty('JETBRAINS_CHANNEL') ? project.getProperty('JETBRAINS_CHANNEL') : null
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.ocpsoft.prettytime:prettytime:4.0.1.Final'
    compile 'com.android.tools.ddms:ddmlib:25.1.0'
    compile 'com.google.code.gson:gson:2.6.2'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

apply plugin: 'license'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'

license {
    strictCheck true
    header project.file('licenses/HEADER.txt')
    ext {
        year = Calendar.getInstance().get(Calendar.YEAR)
        name = "Samebug, Inc."
        email = "hello@samebug.io"
    }
    mapping {
        form = 'XML_STYLE'
    }

    include "**/*.java"
    include "**/*.form"
    exclude "**/*Test.java"
}

group = 'com.samebug.clients.idea'
description = 'Samebug for IntelliJ IDEA'
version '0.12.0-SNAPSHOT' + ( project.hasProperty('buildNumber') ? '+' + project.getProperty('buildNumber') : '')

task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()
}

